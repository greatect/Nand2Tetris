// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Put your code here:
    DMux(in=load, sel=address[14], a=ram, b=device);
    DMux(in=device, sel=address[13], a=screen, b=remaining);
    Or8Way(in=address[0..7], out=orfront);
    Or8Way(in[0]=orfront, in[1..7]=address[6..12], out=orall);
    DMux(in=remaining, sel=orall, a=kbd, b=invalid);

    RAM16K(in=in, load=ram, address=address[0..13], out=ramout);
    Screen(in=in, load=screen, address=address[0..12], out=screenout);
    Keyboard(out=kbdout);

    Mux16(a=kbdout, b=false, sel=orall, out=phase1);
    Mux16(a=screenout, b=phase1, sel=address[13], out=phase2);
    Mux16(a=ramout, b=phase2, sel=address[14], out=out);
}
